manifest {
    mainScript = "main.nf"
    defaultBranch = "master"
    nextflowVersion = ">=20.07.0"
}



params {
    input {
        dir = "input"
        gmy_files = "*.gmy"
        xml_files = "*.xml"
        conditions_file = "conditions.txt"
        trials = 1
    }

    output {
        dir = "output"
    }
}



report {
    enabled = true
    file = "${params.output.dir}/reports/report.html"
}



timeline {
    enabled = true
    file = "${params.output.dir}/reports/timeline.html"
}



trace {
    enabled = true
    fields = "task_id,hash,native_id,process,tag,name,status,exit,module,container,cpus,time,disk,memory,attempt,submit,start,complete,duration,realtime,queue,%cpu,%mem,rss,vmem,peak_rss,peak_vmem,rchar,wchar,syscr,syscw,read_bytes,write_bytes,vol_ctxt,inv_ctxt,workdir,scratch,error_action"
    file = "${params.output.dir}/reports/trace.txt"
    raw = true
}



process {
    errorStrategy = "ignore"
}



profiles {
    pbs {
        process {
            executor = "pbspro"
            time = { (c.hardware_type == "cpu") ? "48h" : "12h" }

            beforeScript = "rm -rf results"
            afterScript  = "rm -rf results"

            module = { "hemelb/${c.latticetype}" }

            // must specify 2 cores per chunk because of bug in Palmetto scheduler
            clusterOptions = {
                (c.hardware_type == "cpu")
                   ? "-l select=${(c.np.toInteger() + 1).intdiv(2)}:ncpus=2:mpiprocs=2:mem=30gb:phase=16:interconnect=any"
                : ((c.ngpus == "0")
                   ? "-l select=${(c.np.toInteger() + 1).intdiv(2)}:ncpus=2:mpiprocs=2:mem=30gb:ngpus=2:gpu_model=${c.hardware_type}:interconnect=any"
                   : "-l select=1:ncpus=${c.np.toInteger() + 1}:mpiprocs=${c.np}:mem=30gb:ngpus=1:gpu_model=${c.hardware_type}:interconnect=any")
            }
        }
        executor {
            queueSize = 50
        }
    }

    standard {
        process {
            executor = "local"
            cpus = { c.np }
            memory = 16.GB

            beforeScript = "rm -rf results"
            afterScript  = "rm -rf results"
        }
        executor {
            queueSize = 1
        }
    }

    testing {
        process.errorStrategy = "terminate"
    }
}
